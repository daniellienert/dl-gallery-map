prototype(DL.Gallery.Map:Map) < prototype(Neos.Fusion:Template) {
    templatePath = 'resource://DL.Gallery.Map/Private/Templates/NodeTypes/Map.html'

    mapData = Neos.Fusion:Collection {
        collection = ${q(assets).children('[instanceof Neos.MetaData:Image][gpsLatitude]').get()}

        itemName = 'image'
        itemRenderer = DL.Gallery.Map:MapDataEntry

        @process {
            removeLastComma = ${String.trim(value, ',')}
            makeHash = ${'[' + value + ']'}
        }
    }

    mapConfiguration = ${Configuration.setting('DL.Gallery.Map')}
    mapConfiguration.@process.json = ${Json.stringify(value)}

    @cache {
        mode = 'cached'
        entryIdentifier {
            node = ${node}
        }

        entryTags {
            1 = ${'NodeType_Neos.MetaData:Image'}
        }
    }
}

prototype(DL.Gallery.Map:MapDataEntry) < prototype(Neos.Fusion:Value) {
    @context.image = ${q(image)}

    data = Neos.Fusion:RawArray {
        title = ${image.property('title')}

        // uh lat and long are switched :|
        gpsLatitude = ${image.property('gpsLongitude')}
        gpsLongitude = ${image.property('gpsLatitude')}

        thumb = Neos.Neos:ImageUri {
            asset = ${image.property('assetObject')}
            width = 200
            height = 200
            allowCropping = true
            allowUpScaling = true
        }
    }

    value = ${Json.stringify(this.data) + ','}
}
